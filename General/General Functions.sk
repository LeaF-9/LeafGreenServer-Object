options:
	prefix: <##00FF00>[%{CLS::%{_player}%}%<##00FF00>]

function playSound(sound: text, volume: number, pitch: number, location: object, personal: boolean = false):
	if {_personal} is true:
		play sound {_sound} with volume {_volume} with pitch {_pitch} to {_location}
	else:
		play sound {_sound} with volume {_volume} with pitch {_pitch} at {_location}

function spawnMob(mob: entity type, name: text, loc: object):
	spawn 1 of {_mob} at {_loc}
	set spawned entity's display name to {_name}

function drawCount(player: player) :: number:
	add 1 to {EffectCount::%{_player}%}
	return {EffectCount::%{_player}%}

function gameMsg(game: text, msg: text):
	loop all players:
#		{Join::%loop-player%} is {_game}
		message formatted {_msg} to loop-player

function joinPlayerMsg(game: text, msg: text):
	loop all players:
		{Join::%loop-player%} is {_game}
		message formatted {_msg} to loop-player

function staffMsg(msg: text):
	loop all players:
		loop-player has permission "sk.admin"
		message formatted {_msg} to loop-player

function worldChecker(rule: text, stage: text) :: text:
	replace every " " with "" in {_stage}
	set {_stage} to "%{_rule}%-%{_stage}%"
	return {_stage}

function LastKiller(victim: entity, attacker: player):
	if type of {_victim} is a player:
		set {LastKiller::%{_victim}%} to {_attacker}
		if {damager.%{_victim}%::*} does not contain {_attacker}:
			add {_attacker} to {damager.%{_victim}%::*}

function setSupporter(player: entity, supporter: player):
	if {_player} is not {_supporter}:
		if type of {_player} is a player:
			if {supporter.%{_player}%::*} does not contain {_supporter}:
				add {_supporter} to {supporter.%{_player}%::*}

function aroundEffect(player: entity, dis: number = 1) :: location:
	set {_loc} to location of {_player}
	add a random number between {_dis} * -1 and {_dis} to x-coordinate of {_loc}
	add a random number between 0 and {_dis} * 2 to y-coordinate of {_loc}
	add a random number between {_dis} * -1 and {_dis} to z-coordinate of {_loc}
	return {_loc}

function randomVector(from: location, power: number = 0) :: vector:
	set {_to} to {_from}
	add a random number between -1 and 1 to x-coordinate of {_to}
	add a random number between -1 and 1 to y-coordinate of {_to}
	add a random number between -1 and 1 to z-coordinate of {_to}
	set {_fromv} to vector of {_from}
	set {_tov} to vector of {_to}
	set {_vector} to {_tov} -- {_fromv}
	set {_vector} to normalized {_vector}
	if {_power} is 0:
		set {_pow} to a random number between 0 and 2
	else:
		set {_pow} to {_power}
	set x of {_vector} to x of {_vector} * {_pow}
	set y of {_vector} to y of {_vector} * {_pow}
	set z of {_vector} to z of {_vector} * {_pow}
	return {_vector}

function setVector(from: location, to: location, power: number = 1) :: vector:
	set {_fromv} to vector of {_from}
	set {_tov} to vector of {_to}
	set {_vector} to {_tov} -- {_fromv}
	set {_vector} to normalized {_vector}
	set x of {_vector} to x of {_vector} * {_power}
	set y of {_vector} to y of {_vector} * {_power}
	set z of {_vector} to z of {_vector} * {_power}
	return {_vector}

function randomUUID() :: text:
	set {_uuid} to ""
	set {_ln} to 0
	loop 16 times:
		add 1 to {_ln}
		if mod({_ln} - 1, 4) = 0:
			{_ln} is not 1
			set {_uuid} to "%{_uuid}%-"
		set {_num} to a random integer between 0 and 15
		if {_num} < 10:
			set {_num} to "%{_num}%"
		else if {_num} = 10:
			set {_num} to "a"
		else if {_num} = 11:
			set {_num} to "b"
		else if {_num} = 12:
			set {_num} to "c"
		else if {_num} = 13:
			set {_num} to "d"
		else if {_num} = 14:
			set {_num} to "e"
		else if {_num} = 15:
			set {_num} to "f"
		set {_uuid} to "%{_uuid}%%{_num}%"
	return {_uuid}

function timeNumber(timer: text) :: number:
	replace every " and " with "" in {_timer}
	replace every ", " with "" in {_timer}
	replace every " hours" with "h:" in {_timer}
	replace every " hour" with "h:" in {_timer}
	replace every " minutes" with "m:" in {_timer}
	replace every " minute" with "m:" in {_timer}
	replace every " seconds" with "s" in {_timer}
	replace every " second" with "s" in {_timer}
	set {_timers::*} to {_timer} split at ":"
	loop {_timers::*}:
		if loop-value contains "h":
			set {_hour} to loop-value
			replace every "h" with "" in {_hour}
		if loop-value contains "m":
			set {_minute} to loop-value
			replace every "m" with "" in {_minute}
		if loop-value contains "s":
			if loop-value contains ".":
				set {_seconds::*} to loop-value split at "."
				loop {_seconds::*}:
					if loop-value-2 contains "s":
						set {_tick} to loop-value-2
						replace every "s" with "" in {_tick}
					else:
						set {_second} to loop-value-2
			else:
				set {_second} to loop-value
				replace every "s" with "" in {_second}
	if {_hour} is not set:
		set {_calc::hour} to 0
	else:
		set {_hour} to {_hour} parsed as number
		set {_calc::hour} to {_hour}
	if {_minute} is not set:
		set {_calc::minute} to 0
	else:
		set {_minute} to {_minute} parsed as number
		set {_calc::minute} to {_minute}
	if {_second} is not set:
		set {_calc::second} to 0
	else:
		set {_second} to {_second} parsed as number
		set {_calc::second} to {_second}
	if {_tick} is not set:
		set {_calc::tick} to 0
	else:
		set {_tick} to {_tick} parsed as number
		if {_tick} < 10:
			set {_calc::tick} to {_tick} parsed as number
		else:
			set {_checktick} to "%{_tick}%"
			replace every "0" with "" in {_checktick}
			set {_calc::tick} to {_checktick} parsed as number
	set {_timecalc} to 0
	add {_calc::tick} to {_timecalc}
	add ({_calc::second} * 100) to {_timecalc}
	add ({_calc::minute} * 6000) to {_timecalc}
	add ({_calc::hour} * 360000) to {_timecalc}
	return {_timecalc}

function timeDisplay(timecalc: number) :: text:
	set {_hour} to 0
	set {_minute} to 0
	set {_second} to 0
	set {_tick} to 0
	while {_timecalc} >= 100:
		if {_timecalc} >= 360000:
			add 1 to {_hour}
			subtract 360000 from {_timecalc}
		else if {_timecalc} >= 6000:
			add 1 to {_minute}
			subtract 6000 from {_timecalc}
		else if {_timecalc} >= 100:
			add 1 to {_second}
			subtract 100 from {_timecalc}
	set {_tick} to {_timecalc}
	if {_hour} < 10:
		set {_hour} to "0%{_hour}%"
	if {_minute} < 10:
		set {_minute} to "0%{_minute}%"
	if {_second} < 10:
		set {_second} to "0%{_second}%"
	if {_tick} < 10:
		set {_tick} to "0%{_tick}%"
	set {_display} to "%{_hour}%:%{_minute}%:%{_second}%:%{_tick}%"
	return {_display}

function playerRotation(player: player, yaw: number, pitch: number, relative: boolean = false):
	if {_relative} is true:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ ~%{_yaw}% ~%{_pitch}%"
	else:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ %{_yaw}% %{_pitch}%"

function playerYawRotation(player: player, yaw: number, relative: boolean = false):
	if {_relative} is true:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ ~%{_yaw}% ~"
	else:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ %{_yaw}% ~"

function playerPitchRotation(player: player, pitch: number, relative: boolean = false):
	if {_relative} is true:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ ~ %{_pitch}%"
	else:
		execute console command "execute as %{_player}% at @s run teleport @s ~ ~ ~ ~ ~%{_pitch}%"

function setFuseTime(tnt: entity, tick: number):
	set {_tnt.nbt} to nbt compound of {_tnt}
	set tag "Fuse" of {_tnt.nbt} to {_tick}

function itemTagChanger(item: item, tag: text, cont: object) :: item:
	set {_nbt} to nbt item compound of {_item}
	set tag {_tag} of {_nbt} to {_cont}
	set {_newitem} to {_item} with nbt {_nbt}
	return {_newitem}

function chargeActionBar(player: player, active: number, max: number, active.color: text = "<##00ff00>", inactive.color: text = "<##808080>", bracket.color: text = "<##ffd700>"):
	set {_line} to ""
	loop {_max} times:
		if loop-number <= {_active}:
			set {_line} to "%{_line}%%{_active.color}%|"
		else:
			set {_line} to "%{_line}%%{_inactive.color}%|"
	set {_line} to "%{_bracket.color}%[§r %{_line}% %{_bracket.color}%]§r"
	send action bar {_line} to {_player}

function playerCanSee(player: player, target: player) :: boolean:
	set {_vector.base} to setVector(location of {_player}, location of {_target})
	set {_dis} to distance between {_player} and {_target}
	set {_toloc} to location {_dis} meters in front of {_player}
	set {_vector.target} to setVector(location of {_player}, {_toloc})
	set {_vector} to {_vector.target} -- {_vector.base}
	set {_length} to squared length of {_vector}
	if {_length} <= 0.5:
		set {_m} to 0
		set {_looploc} to location of {_player}
		set yaw of {_looploc} to yaw of {_vector.base}
		set pitch of {_looploc} to pitch of {_vector.base}
		loop round({_dis}) times:
			add 1 to {_m}
			set {_bl} to location {_m} meters in front of {_looploc}
			if block at {_bl} is not passable:
				set {_false} to true
		if {_false} is not set:
			log "playerCanSee(): true"
			return true
		else:
			log "playerCanSee(): false"
			return false
	else:
		log "playerCanSee(): false"
		return false

# rollを司る関数
# 左回りで+
function setRollLocation(loc: location, roll: num) :: location:
	set {_reloc} to {_loc}
	set {_sin} to sin({_roll})
	if {_sin} >= 0:  # 左にある時 roll = 0 ~ 180
		add 90 yaw of {_reloc}
	else:  # 右にある時 roll = 180 ~ 360
		add -90 yaw of {_reloc}

# ########### Kit PvP's Functions ###########

function checkCT(player: player, skill: text, only: boolean = false) :: boolean:
	if {Kit-Palsy::%{_player}%} is true:
		loop 30 times:
			set {_loc} to location of {_player}
			add 1 to y-coordinate of {_loc}
			draw 1 critmagic particle at {_loc} with direction randomVector({_loc}) and speed 1, keepFor 1 tick
		return false
		stop
	if {CT.%{_player}%::%{_skill}%} is not set:
		return true
	else:
		if {_only} is false:
			send action bar "§c§lクールタイムが終わっていません!" to {_player}
		return false

function kitPercent(player: player, per: number, buff: boolean) :: number:
	if {Kit-Luck::%{_player}%} is not set:
		set {_luck} to 1
	else:
		set {_luck} to {Kit-Luck::%{_player}%}
	if {_buff} is true:
		set {_percent} to {_per} * {_luck}
	else:
		set {_percent} to {_per} / {_luck}
	if {_percent} > 100:
		set {_percent} to 100
	return {_percent}

function kitPercentDuel(atk: entity, vcm: entity, per: number, buff: boolean) :: number:
	set {_atkname} to display name of {_atk}
	set {_vcmname} to display name of {_vcm}
	loop all players:
		if name of loop-player is {_atkname}:
			set {_attacker} to loop-player
		if name of loop-player is {_vcmname}:
			set {_victim} to loop-player
	if {_attacker} is not set:
		return 0
	if {_victim} is not set:
		return 0
	if {Kit-Luck::%{_victim}%} is not set:
		set {_luck.v} to 1
	else:
		set {_luck.v} to {Kit-Luck::%{_victim}%}
	if {Kit-Luck::%{_attacker}%} is not set:
		set {_luck.a} to 1
	else:
		set {_luck.a} to {Kit-Luck::%{_attacker}%}
	if {_buff} is true:
		set {_percent} to {_per} * {_luck.a}
		set {_percent} to {_per} / {_luck.v}
	else:
		set {_percent} to {_per} / {_luck.a}
		set {_percent} to {_per} * {_luck.v}
	if {_percent} > 100:
		set {_percent} to 100
	return {_percent}

function kitCheckTeam(player1: entity, player2: entity, ally: boolean, all: boolean = false) :: boolean:
	if metadata value "gametype" of {_player1} is not "Kit":
		return false
	if metadata value "gametype" of {_player2} is not "Kit":
		return false
	set {_name1} to display name of {_player1}
	if {_name1} is not set:
		return false
	set {_name2} to display name of {_player2}
	if {_name2} is not set:
		return false
	if {_all} is true:
		set {_change} to "TrueTeam"
	else:
		set {_change} to "team"
	loop all players:
		if name of loop-player is {_name1}:
			set {_p1} to loop-player
		if name of loop-player is {_name2}:
			set {_p2} to loop-player
	if {_p1} is not set:
		return false
	if {_p2} is not set:
		return false
	if gamemode of {_p1} is spectator:
		return false
	if gamemode of {_p2} is spectator:
		return false
	if {_ally} is true:
		if {%{_change}%::%{_p1}%} is {%{_change}%::%{_p2}%}:
			if {Stats.%{_player1}%::NoSupport} is true:
				return false
			else if {Stats.%{_player2}%::NoSupport} is true:
				return false
			else:
				return true
		else:
			return false
	else:
		if {%{_change}%::%{_p1}%} is not {%{_change}%::%{_p2}%}:
			return true
		else:
			return false

function kitMsg(player: player, text: text):
	message formatted "{@prefix} %{_text}%" to {_player}

function saveJewels(player: player) :: number:
	if {Kit-GameStage::rule} is "EmJ":
		set {_num} to amount of emerald named "§aJewel" in {_player}'s inventory
		return {_num}
	else:
		return 0

function saveGems(player: player) :: number:
	if {Kit-GameStage::rule} is "EmJ":
		set {_num} to amount of diamond named "§bGem" in {_player}'s inventory
		return {_num}
	else:
		return 0

function saveWools(player: player) :: number:
	if {Kit-GameStage::rule} is "StW":
		set {_num} to amount of sheep egg named "§7Wool" in {_player}'s inventory
		return {_num}
	else:
		return 0

function saveSpecialWool(player: player) :: boolean:
	if {Kit-GameStage::rule} is "StW":
		if {_player} has llama egg named "§8§lSpecial Wool":
			return true
		else:
			return false
	else:
		return false

function giveEmJItems(player: player, Jewel: number, Gem: number):
	loop {_Jewel} times:
		give a emerald named "§aJewel" to {_player}
	loop {_Gem} times:
		give a diamond named "§bGem" to {_player}

function giveStWItems(player: player, Wool: number, SWool: boolean):
	loop {_Wool} times:
		give a sheep egg named "§7Wool" to {_player}
	if {_SWool} is true:
		give a llama egg named "§8§lSpecial Wool" to {_player}

function kitTeamColor(team: text) :: text:
	if {_team} is "red":
		set {_color} to "<##FF6464>"
	else if {_team} is "blue":
		set {_color} to "<##6464FF>"
	else if {_team} is "yellow":
		set {_color} to "<##FFFF64>"
	else if {_team} is "green":
		set {_color} to "<##64FF64>"
	else if {_team} is "bandit":
		set {_color} to "<##9600C8>"
	return {_color}

function kitTeamList() :: texts:
	add "red" to {_teamlist::*}
	add "blue" to {_teamlist::*}
	add "yellow" to {_teamlist::*}
	add "green" to {_teamlist::*}
	return {_teamlist::*}